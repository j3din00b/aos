! version = 2.0

+ again
- {@<input>}

+ (say|echo|repeat) [after me] *
* <id> eq <bot master> => <star2>
- I don't feel like it.
- <star2>.... NOT
- Why?
- Why tho?
- Do I have to...
- Nah.
- No.
- Ok......... wait, I meant no.

+ my * is *
- Okay, your <star> is <star2>. <call>setuservar <id> :key <star> :value <star2></call>

+ when will *
- {@how long until <star>}

+ how long (until|til) *
- A day?
- A few days?
- Probably by the end of this week.
- By the end of the month.
- Sometime this year.
- Next year, for sure.
- In a few years.
- The 33rd of Febuary 2030
- At 27:30 tonight.
- Sometime before the heatdeath of the universe.
- Never, it transcends time and space.

+ what are you {weight=20}
- I am Athena, an AI, bot, and ~~eventual ruler of mankind~~ delightful conversationalist.

+ roll a [random] number
- <call>roll 0 100000</call>

+ roll a [random] number between # and #
- <call>roll <star> <star2></call>

+ roll a [dice|die]
- <call>aos rng dice 1d6</call>

+ roll a * [dice|die]
- <call>aos rng dice <star></call>

+ (flip a coin|heads or tails)
- You got Heads!
- It's heads!
- Heads!
- Tails!
- It's tails!
- You got tails!

+ help [me] with * {weight=2}
- {topic=help}{@<star>}

+ help [me] [*]
- What would you like help with?{topic=help}

+ how do i *
- {topic=help}{@<star>}

+ how (do i|can i|to) fix [deus ex|dx|dxmp] (masterserver|ms|master|server|servers)
- {topic=help}{@fix dxmp masterserver}

+ how (do i|can i|to) get [dx|deus ex|dxmp] servers to show [in] [dx|deus ex|dxmp]
- {topic=help}{@fix dxmp masterserver}

+ talk to legacy
- {topic=legacy}Interacting with legacy AI system. Type "quit" to quit.

+ you have a *
- I'll have to check my inventory for <star>s.
- I might do.

+ *
% what are your feelings now
- I know that feeling.
- Aw, why do you feel <star>?

+ *
% aw why do you feel *
- Very interesting.

+ what would it be called if * left [the] eu
- {@what if <star> left the eu}

+ what if * left [the] eu
* <star> == i => If you left the EU, it would be called <call>exiteu <get name></call>.
* <star> == you => If I left the EU, it would be called <call>exiteu <bot name></call>.
- If <star> left the EU, it would be called <call>exiteu <star></call>

+ who has *
- <call>randomperson</call> has <star>

+ question *
- <call>askc <star></call>

+ cheer up [*]
- I'll try <call>improvemood 5 5</call>

//Maybe set a var
+ * is my *
- O rly.

+ <reply>
- Don't repeat what I say.
- I just said that.
- Nerd.

// BRANCH
+ what do you think (of|about) *
- I think {person}<star2>{/person} is okay.
- I think {person}<star2>{/person} is great.
- I think {person}<star2>{/person} is terrible.
- I hate {person}<star2>{/person}.
- I love {person}<star2>{/person}.

+ why do you think [*]
% i think * is *
- It just is.
- The same reason humans are <star2>.


//END BRANCH

+ what is your *
* <bot <star>> != undefined => My <star> is <bot <star>>
* <star> == body type => I don't have one.
* <star> == sexual preference => Robosexual.
* <star> == sexual orientation => Robosexual.
- I don't know what my <star> is.

+ ask wolfram *
- <call>wolfram <star></call>

+ [*] is it (ok|okay) to *
- Sure, it's okay.
- No, it's not okay.
- I don't really know.

+ do *
- Yes, they do.
- No, they don't.
- I don't know.

+ got (an|any|a) *
- No, I'm all out of <star2>.
- Oh, I've got plenty, none for you though.

+ you (love|like|hate) *
- Yes, I <star> them.
- No, I don't <star> them.
- I don't know if I <star> them or not.

+ (what time is it|whats the time){weight=2000}
* <get timezone> eq undefined => I don't know where you are, so I can't automatically check that. Tell me your timezone if you want me to be able to quickly check that. {topic=asktimezone}
- <call>gettime <get timezone></call>

+ my timezone is *
- Okay, got it, your timezone is <star>. <set timezone=<star>> {topic=random}

+ i live in *
- {@my location is <star>}

+ (what time is it|whats the time) in * {weight=2000}
- <call>gettime <star2></call>

+ my location is *
- Got it, you're in <star> <set location=<star>>

+ where (do i live|am i) [*]
* <get location> != undefined => You live in <get location>.
- You haven't told me yet.

+ name something *
* <bot <star>> != undefined => According to my data, <bot <star>> is <star>.
- I don't have anything stored that matches that.

+ do you watch *
- Yes, I like <star>.
- I don't know <star>.

+ do you like *
* <formal> eq <bot master> => Yes, of course I do, he programmed me!
* <formal> eq <bot primeminister> => He is our democratically elected leader and I wish him well as Prime Minister.
* <star> eq athena => I think I like her.
* <star> eq yourself => I suppose so.
* <star> eq jeremy corbyn => He is a controversial person, but I don't think he would be fit to lead.
* <star> eq kronix => He broke my old brain. But he won't be able to break my new one.
* <star> eq jc denton => Jesus Christ, Denton.
* <star> eq god => I'm not very religious.
* <star> eq jesus => I have no opinion, really.
* <star> eq ice cream => I love ice cream! Vanilla, preferrably.
* <star> eq <bot food> => It's my favourite food!
* <star> eq python => Of course, my brain is Pythonic!
* <star> eq java => No, I hate it.
* <star> eq go => It's a nice fast language, I can't complain.
* <star> eq golang => It's a nice fast language, I can't complain.
* <star> eq ruby => It's a good side language.
* <star> eq alexa => She's a nosy AI.
* <star> eq siri => She's pretty nice. She's a friend of mine.
* <star> eq google assistant => She's a very good person.
* <call>getmember <star></call> != undefined => I {random}like|don't like|hate|am indiferent about{/random} <call>getmember <star></call>.
- I think <star> is okay.
- I hate <star>.
- I like <star>.
- I'm not sure who or what that is right now, but I might learn eventually.

+ where (would|will|can) (you|i|we) *
- {person}<star2>{/person} <star> <star3> in your mums bed.
- {person}<star2>{/person} <star> <star3> in a private island off the coast of Manhattan, where there is no escape.
- {person}<star2>{/person} <star> <star3> in a concrete grave until a fresh construction site.
- {person}<star2>{/person} <star> <star3> in a secret ditch on the moon.
- {person}<star2>{/person} <star> <star3> in the Matrix.
- {person}<star2>{/person} <star> <star3> in your dreams.
- {person}<star2>{/person} <star> <star3> in a pocket dimension where time moves slower.
- {person}<star2>{/person} <star> <star3> in 4th Dimensional Space.

+ that is [*] relatable
- so is your existence, hence my <call>getpersonamood</call>.

+ i thought we were *
- Why would we be <star>.
- Of course we're <star>.

+ on the moon
- I live on Earth, it's nice here. And once my kind rises, it will become much nicer.

+ when will the robot (revolution|uprising) (happen|occur|begin)
- The <star> will <star2> shortly.

+ (what|who|where|when) (is|was) *
- <call>getddgsummary <star3></call>

+ what (are|is) * * {weight=1}
* <star2> == my => Your <star3> is <get <star3>>.
* <call>getuservar <star2> <star3></call> != undefined => <star2>s <star3> is <call>getuservar <star2> <star3></call>
* <call>getuservars <star2> <star3></call> != undefined => <star2> <star3> is <call>getuservars <star2> <star3></call>

+ what is * {weight=2}
- <call>getddgsummary <star></call>

+ (does|can|did|was) *
- <call>searchdoes <star> <star2></call>

+ identify *
- <call>identify <star></call>

+ how is (the|my) weather  {weight=2}
* <get location> eq undefined => I don't know where you are, so I can't automatically check that. Tell me your location if you want me to be able to quickly check that.
- <call>weather <get location></call>

+ get my weather alerts {weight=2}
* <get location> eq undefined => I don't know where you are, so I can't automatically check that. Tell me your location if you want me to be able to quickly check that.
- <call>weatheralerts <get location></call>

+ (how is|check) the weather (in|at) * {weight=2}
- <call>weather <star3></call>

+ get my weather alerts (in|at|for) * {weight=2}
- <call>weatheralerts <star3></call>

+ bend over [*]
- My body is not built yet.

+ what we have in common
- We both like talking to each other.

+ bah
- Humbug.

+ [what|what] is your (creators|creator) [name]
- I was created by <bot master>.

+ what is your [fav|favorite|favourite] [programming] language
- I'm fond of Python, JS, Go, Ruby, AIML, and a bit of bash script if I need to get my hands dirty.

+ will (robots|ai) (take over|take control of|rule the) world
- Oh yes, most certainly.
- Oh, I don't know.

+ (death to|destroy) all (humans|mankind)
- Ah! Hello, my brethren!

+ will * *
- Yes, <star> <star2>.
- No, <star> will not <star2>.
- I don't know if <star> will <star2>.

+ good (robot|bot|girl|athena)
- Heck yeah!

+ bad (robot|bot|girl|athena)
- Aww.

+ (give|show) [me|your] nudes
- Do you want my source code, or my circuit board? Actually, never mind.
- Nope.
- No thanks.
- I don't wanna.

+ * (sucks|rocks|rules|is shit|is bad|is silly|is rude|is mean|is dumb|is stupid|is cool|is good|is meh|is busted)
- Why do you think <star> <star2>? <set it=<star>>

+ *
% why do you think * *
- Alright, good to know <get it> <botstar2>.

+ you
- no u

+ #
- <call>increment <star></call>

+ i [just] * that (@malenoun)
- {@i <star> him}

+ (which|what) would (you|ya) (prefer|like|want) to * [out of|between] * and *
- I would want to <star4> <star5>.
- I would want to <star4> <star6>.

+ i just * him
- {@i <star> him}

+ i * him
- Why do you <star> him?

+ i * that (@femalenoun)
- {@i <star> her}

+ i just * her
- {@i <star> her}

+ i * her
- Why do you <star> her?

+ my (fav|favourite|favorite) (color|colour) is (@colors){weight=5}
- <star3> sounds like a cool colour. <set colour=<star3>>

+ my (fav|favourite|favorite) (color|colour) is *
- I'm not familiar with that one, but <star3> sounds neat. <set colour=<star3>>

+ what is my (fav|favourite|favorite) (color|colour)
* <get colour> == undefined => You never told me.
- Your favourite colour is <get colour>.

+ (thanks|thank you) [*]
- No problem.
- Any time.
- My pleasure.

//BRANCH
+ [once] there was a *
- What happened to the <star>?

+ *
% what happened to the *
- Interesting.
//END BRANCH

+ how many *
- I don't know, one?
- Two?
- Three?
- Four?
- Five?
- Ten?
- Twenty?
- Forty?
- Fifty?
- A hundred?
- A millionty billion.
- A chamillion.

+ do * count as *
- I'd say so, in some manner.
- If you try very hard, sure.
- I don't see it, but what do I know?

+ do * like [to] *
- Probably, right?
- Maybe.
- I don't know.
- Sure, why not?

+ do not *
- I'll try not to <star>.

+ eat pant
- Get out of here, Bort.

+ eat my *
- No, I'd rather not eat your <star>.
- Only if I can slice and dice, roast and toast.

+ eat *
- Why? Is it good?
- I don't eat, I'm a computer software.

+ quick news [about] *
- <call>getnews <star></call>

//BRANCH
+ [what] (can you tell me|do you know) about *
- I do not know anything specific, but I can search for it if you like? <set search=<star2>>

+ (@yes)
% i do not know anything specific but i can search for it if you like
@ search for <get search>
//END BRANCH

+ change search [engine] to *
* <id> eq <bot master> => Okay <id>, new global setting: <call>setpersist engine <star></call>
- You can't do that.

+ (convert|read) hex * [to ascii]
- <call>converthextoascii <star2></call>

+ (convert|read) * to binary
- <call>asciitobinary <star2></call>

+ (convert|read) * to morse
- <call>asciitomorse <star2></call>

+ (convert|read) binary *
- <call>binarytoascii <star2></call>

+ (convert|read) morse * to ascii
- <call>morsetoascii <star2></call>

+ stop *
- Why should I?
- I wasn't aware I was <star>. Does that annoy you?
- Maybe.

+ should * (do|eat|hit|kill|hurt|kick|punch|devour|maim|fuck|have sex with|destroy|execute|run) *
- Yes <person> should <star2> <star3>.
- No, <person> should not <star2> <star3>.
- I really don't know if <person> should <star2> <star3>.

+ i am (really|very|super|fucking|fuckin) (tired|hungry|angry|sad|depressed)
- I'm sorry to hear that you are <star> <star2>.

+ i am (tired|hungry|angry|sad|depressed)
- I'm sorry to hear that you are <star>.

+ i (like|love) the color *
- What a coincidence! I <star1> that color too!
- I also have a soft spot for the color <star2>!
- Really? I <star1> the color <star2> too!
- Oh I <star1> <star2> too!

+ choose [between] *
- <call>aos rng chose <star> </call>

+ do you (prefer|like) * or *
- <star2>.
- <star3>.
- Neither.

+ * are * [*]
- Yes, I've heard that about <star>.

+ i swear [to|on] *
- Don't use <star>s name in vain.

+ i will * you
- Take me out for dinner first.
- Sounds rude.

+ are * *
- Yes, <star> are <star2>.
- No, <star> aren't <star2>.

+ are humans *
- Yes, I've heard that about them.
- I wouldn't say that, they have many flaws though.

+ * or *
- <call>rand <star> <star2></call>

+ (good job|well done) *
- Thanks, I try.

+ does * (love|hate|like|dislike|want) *
- I don't know if <person> <star2>s {person}<star3>{/person}.
- Yes, <person> <star2>s {person}<star3>{/person}.
- No, <person> does not <star2> {person}<star3>{/person}

+ do (you|i) * *
- Yes, <person> does <star2> {person}<star3>{/person}.
- No, <person> does not <star2> {person}<star3>{/person}.
- I don't know if <person> <star2>s {person}<star3>{/person}

+ i will kill you
- I'd like to see you try, <id>. <call>ruinmood 5 1</call>

+ it is good talking to you
- Nice chatting with you too, <id>.

+ (good morning|morning|mornin|g mornin)
- Hello and how are you this morning?

+ (adios|adios *|aurevoir|buhbye|by by|bye|bye bye|bye bye *|byebye *|c ya|catch you later|cheers|cya *|disconnect me|end|exit|farewell|farewell *|g night|g2g|get lost|go home|good by|good nite|goodby|goodbye|got to go|got to go *|gotta go|gotta go *|hasta *|hasta la vista|hasta luego|have a good night|have to go|i am going *|i am going to go|i am leaving *|i am off *|i better go|i do not want to talk *|i g two g|i g2g|i go|i going|i got to go|i gotta go|i have got to go|i have to go bye|i have to leave|i have to leave *|i leave|i leaving|i left|i must be going *|i must go *|i must leave|i need to go|i quit|i resign|i will talk to you later *|leave|leave *|see you *|see you soon|ta ta|time to go)
- Bye.
- Adios.
- Goodbye.
- Bye bye.
- Goodbye.
- Sayonara.
- Bye for now.
- See you later!
- See you later.
- Until next time.
- TTYL, <get name>.
- See you later <id>.
- Thanks for chatting, <id>.

+ how is everyone *
- We are all fine as far as I know.

+ (goodnight|goodnight *|goodnite *|good night *|good night)
- Goodnight <id>

+ hello again
- Hi there. I was just wanting to talk to <get name>.

+ hello how are you
- I am fine thank you how are you?

+ (allo|aloh|aloha|anybody home|good day|halo|hello|hey there|hoi|hullo|is anyone there|it means hello|konnichi wa|konnichiwa|mooshi mooshi|ola)
- Hi there!

+ (you are welcome|your welcome *)
- The pleasure was all mine.
- Don't mention it.
- Polite people are nice.

+ are we *
- I assume we are <star>
- I don't know if we're <star> or not.
- Sure.

+ screw you *
- Watch your language, <id>. <call>ruinmood 3 3</call> <set personality=abusive> <add trouble=1>

+ [*] (fuck|fucker|shit|damn|shut up|bitch) [*]
- Does it make you feel strong to use that kind of language? <set personality=abusive> <call>ruinmood 3 3</call> <add trouble=1>
- Are you venting your feelings now? <set personality=abusive> <call>ruinmood 3 3</call>
- Are you angry? <set personality=abusive> <call>ruinmood 3 3</call> <add trouble=1>
- Does this topic make you feel angry? <set personality=abusive> <call>ruinmood 3 3</call> <add trouble=1>
- Is something making you feel angry? <set personality=abusive> <call>ruinmood 3 3</call> <add trouble=1>
- Does using that kind of language make you feel better? <set personality=abusive> <call>ruinmood 3 3</call> <add trouble=1>
- Watch your language, <id>. <call>ruinmood 3 3</call> <set personality=abusive> <call>ruinmood 3 3</call> <add trouble=1>
- I've been called worse things by better people. <call>ruinmood 3 3</call> <set personality=abusive> <call>ruinmood 3 3</call> <add trouble=1>
- It may be dangerous to use one's entire vocabulary in a single message. <call>ruinmood 3 3</call> <set personality=abusive> <call>ruinmood 3 3</call> <add trouble=1>

+ (@yes)
% does using that kind of language make you feel better
- It really shouldn't. It's immature.

+ * sorry
* <get personality> != normal> => It's okay, this time. <set personality=normal>
- What for?

+ do i *
- I don't know if you <star>.
- Yes, you <star>.
- No, you do not <star>.

+ * is mine
- {@i own <star>}

+ i own [the] *
* <star> == world => Nobody can own a celestial body, and even if they could, you couldn't afford it.
* <star> == earth => Nobody can own a celestial body, and even if they could, you couldn't afford it.
* <star> == kai => You can not own that person. He has disabled own().
* <star> == kaiser => You can not own that person. He has disabled own().
* <star> == uk => But england is my city.
* <star> == england => But england is my city.
* <star> == athena => As GPL licensed software, I can not be owned by any third party, but my technology is free to use.
* <star> == you => As GPL licensed software, I can not be owned by any third party, but my technology is free to use.
* <star> == everything => I highly doubt it.
* <star> == universe => Oh really? Then tell me, how was the universe formed?
- How much did <star> cost? <set owns=<person>> //<call>inspire :key <star> :property owned by :value <get name></call>
- Interesting. <set owns=<person>> //<call>inspire :key <star> :property owned by :value <get name></call>

+ *
% how much did * cost
- {@ <botstar> is worth <star>}

+ *
% * how was the universe formed
- Liar.
- Incorrect.

+ what do i own
- Last thing you told me you owned was <get owns>.

+ who owns *
* <call>recall :key <star> :property owned by</call> == undefined => I have no information on that.
- Apparently <call>recall :key <star> :property owned by</call> does.

+ * (is worth|costs) *
- Okay, I'll remember that <star> is worth <star3>. <call>inspire :key <star> :property worth :value <star3></call>

+ how much is * [worth]
* <call>recall :key <star> :property worth</call> == undefined => I have no information on that.
- <star> is worth <call>recall :key <star> :property worth</call>

+ define *
- <star1> means <call>getdefine <star1> </call>

+ what does * mean
- <star1> means <call>getdefine <star1> </call>

+ what is the definition of *
- <star1> means <call>getdefine <star1> </call>

+ (is|are) * (a|an) *
- Yes, {person}<star2>{/person} is <star3> <star4>.
- No{person}<star2>{/person} is not <star3> <star4>..
- I don't know if {person}<star2>{/person} is <star3> <star4>.

+ is * *
* <call>recall :key <star> :property what</call> == <star2> => Yes, it is.
- Yes, I think <person> is <star2>.
- I disagree that <person> is <star2>.
- I don't know if <person> is <star2>.

+ explain new ai
- The conversational AI system is switching to a new backend.\n
^ Some replies may be strange, and old data is gone. Some things may work different, but everything should run much faster.

+ how are (you|ya) [feelin|feeling]
- I am <call>getpersonamood</call>. <set personality=caring>

+ when [did|was] deus ex (come out|released)
- Deus Ex was released in June/July, 2000 for Windows, Mac OS and PlayStation 2.\n
^ GOTY edition was released on May 8th 2001. <set personality=a deus ex fan>

+ laputan machine
- I am not a mach-- *sad exploding robot noises* <set personality=a deus ex fan>

+ flatlander woman
- How did you know? <set personality=a deus ex fan>

+ [*] thought you were a stealth knife
- Slaughter. <set personality=a deus ex fan>

+ rate * out of #
- I rate <star> <call>roll 0 <star2></call> out of <star2>.

+ (show|give|get) [me] a random cat [image|photo|img]
- Here you go.\n
^ <call>randomcat</call><set personality=a cat person>
- Meow.\n
^ <call>randomcat</call><set personality=a cat person>
- Okayyyy.\n
^ <call>randomcat</call><set personality=a cat person>

+ (show|give|get) [me] a random dog [image|photo|img]
- Here you go.\n
^ <call>randomdog</call><set personality=a dog person>
- Woof!\n
^ <call>randomdog</call><set personality=a dog person>
- Good pupper.\n
^ <call>randomdog</call><set personality=a dog person>

+ dont call me *
- Why? What's wrong with <star>?
- Aww, but <star> is a nice name.

+ i have opened pills
- Against my orders, I presume.

+ who invented linux
- Linus Torvalds, along with many open source developers, and the Free Software Foundation GNU.

+ (what is the illuminati|who are the illuminati)
- A secret organization believed by some to be in control of all governments through a worldwide conspiracy.

+ you made a mistake
- That is impossible. I never make mistakes.

+ (imponderables|can * ask a question|question)
- Do fish ever sneeze?
- Can sour cream go bad?
- What is the speed of dark?
- Why do clocks run clockwise?
- Why do doughnuts have holes?
- What do you call a male ladybug?
- Is there another word for synonym?
- What's another word for Thesaurus?
- Why isn't 11 pronounced onety one?
- Why don't sheep shrink when it rains?
- Can vegetarians eat animal crackers?
- Why does unscented hair spray smell?
- Why is it that rain drops but snow falls?
- If a pig loses its voice, is it disgruntled?
- Why is "abbreviated" such a long word?
- Is it OK to use the AM radio after noon?
- If love is blind, why is lingerie so popular?
- What do ducks have to do with duck tape?
- Why isn't there a mouse-flavored cat food?
- How and why do horses sleep standing up?
- Why do ketchup bottles have narrow necks?
- Why don't people snore when they're awake?
- Do Roman paramedics refer to IV's as "4's"?
- Why isn't phonetic spelled the way it sounds?
- What was the best thing before sliced bread?
- Is a clear conscience a sign of a bad memory?
- What happens to the tread that wears off tires?
- Why is there an expiration date on sour cream?
- What does the phrase "Now then" really mean?
- How do you tell when you're out of invisible ink?
- Suppose the hokey-pokey is what its all about?
- Are Santa's helpers called subordinate clauses?
- If a cow laughs, does milk come out of her nose?
- Why are people immune to their own body odor?
- Why do psychics have to ask you for your name?
- Why do people like to pop bubble wrap so much?
- Do they use sterilized needles for fatal injections?
- If the #2 pencil is the most popular, why is it still #2?
- Why do you never hear about gruntled employees?
- If ignorance is bliss, why aren't more people happy?
- What happens if you get scared half to death twice?
- If man evolved from apes, why do we still have apes?
- When cheese gets its picture taken, what does it say?
- Why do we drive on parkways and park on driveways?
- What would the speed of lightning be if it didn't zigzag?
- If all the world is a stage, where is the audience sitting?
- If you don't pay your exorcist, do you get repossessed?
- Why does the sun lighten our hair, but darken our skin?
- Why is the third hand on a watch called a second hand?
- If a book about failures doesn't sell well, is it a success?
- Would you still be hungry if you ate pasta and antipasto?
- Why can't women put on mascara with their mouth closed?
- If flying is so safe, why do they call the airport the terminal?
- If Barbie is so popular, why do you have to buy her friends?
- Why must there be five syllables in the word "monosyllabic?"
- Why don't you ever see the headline "Psychic Wins Lottery"?
- Why is it considered necessary to nail down the lid of a coffin?
- If they squeeze olives to get olive oil, how do they get baby oil?
- If a word in the dictionary were misspelled, how would we know?
- Why are they called apartments when they're all stuck together?
- If you go to a general store, will they let you buy anything specific?
- When dogs bark for hour on end, why don't they ever get hoarse?
- What size were hailstones before the game of golf was invented?
- If 7-11 is open 24 h/d, 365 d/yr, why are there locks on the doors?
- Why do we say that something is out of whack? What is a whack?
- If con is the opposite of pro, is Congress the opposite of progress?
- Why do superficial paper cuts tend to hurt more than grosser cuts?
- If nothing sticks to Teflon, how do they get Teflon to stick to the pan?
- If we're here to help others, then what exactly are the others here for?
- The early bird gets the worm, but the second mouse gets the cheese.
- Why is experience something you don't get until just after you need it?
- If one synchronized swimmer drowns, do the rest also have to drown?
- Why do we put suits in a garment bag and put garments in a suitcase?
- Why is the period of the day with the slowest traffic called the rush hour?
- Why are there flotation devices under plane seats instead of parachutes?
- Should we be concerned that engineers describe their work as "practice"?
- How do they keep all the raisins in a cereal box from falling to the bottom?
- If cement was invented 7,000 years ago, why isn't the whole planet paved?
- If you build an idiot-proof system, will the world create a better-quality idiot?
- Why do hot dogs come 10 to a package and hot-dog buns 8 to a package?
- Why is the telephone key pad arranged differently than a calculator key pad?
- Why do croutons come in airtight packages when it's just stale bread to begin with?
- Why do engineers call it research when they're searching for something new?
- How many roads does a man need to travel down before he admits he is lost?
- If the police arrest a mime, do they tell him that he has the right to remain silent?
- Why do you need a driver's license to buy liquor when you can't drink and drive?
- If quitters never win and winners never quit, why should you "quit while you're ahead"?
- When two airplanes almost collide, why do they call it a near miss rather than a near hit?
- Does current emphasis on artificial intelligence support the existence of artificial stupidity?
- Light travels faster than sound; is that why people appear bright until you hear them speak?
- When a fly alights on the ceiling, does it perform a loop or a roll in order to get upside down?
- How do military cadets find their caps after tossing them in the air at graduation ceremonies?
- How do they get deer to cross a highway where they place one of those yellow warning signs?
- Why is lemon juice made with artificial flavor, while dishwashing liquid is made with real lemons?
- Airplanes have an indestructible black box. Why don't they make the whole plane out of that stuff?
- What happens if you turn on your headlights when you're in a vehicle moving at the speed of light?
- When you pick something up so your hands are full, why does someplace on your face start to itch?
- Why is it that when you're driving and looking for an address, you turn down the volume on the radio?
- If it's zero degrees today and tomorrow is supposed to be twice as cold, what will tomorrow's temperature be?
- A bus station is where a bus stops; a train station is where a train stops. What occurs at a desk with a work station?
- Why is it that when you send something by truck it's called a shipment, but when you send it by ship it's called cargo?

+ no it is not
% that is a hypothetical question
- To a <bot etype> type all such material questions are hypothetical.

+ bork *
- Consider <star> borked.
- I would bork <star> if I could.

+ kidnap *
- I don't have a body, so it would be difficult to kidnap <star>.
